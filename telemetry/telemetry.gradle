buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.openapitools:openapi-generator-gradle-plugin:5.4.0"
  }
}

apply plugin: 'org.openapi.generator'
apply from: "$rootDir/gradle/java.gradle"

ext {
  minimumBranchCoverage = 0.6
  minimumInstructionCoverage = 0.8
  excludedClassesCoverage = [
    'datadog.telemetry.TelemetryRunnable.ThreadSleeperImpl',
    'datadog.telemetry.HostnameGuessing',
    'datadog.telemetry.Uname',
    'datadog.telemetry.Uname.UtsNameMacOs',
    'datadog.telemetry.Uname.UtsNameLinux',
    'datadog.telemetry.Uname.EmptyUtsName',
  ]
  excludedClassesBranchCoverage = [
    'datadog.telemetry.PolymorphicAdapterFactory.1',
    'datadog.telemetry.Uname.UtsNameMapper',
  ]
  excludedClassesInstructionCoverage = []
  minJavaVersionForTests = JavaVersion.VERSION_1_7
}

openApiGenerate {
  generatorName = 'java'
  inputSpec = "$projectDir/src/main/schemas/telemetry-api-v1.yaml"
  outputDir = "$buildDir/generated"
  modelPackage = "datadog.telemetry.api"
  templateDir = "$projectDir/src/main/templates"

  typeMappings = [
    // Java representation of ambiguous types
    "OneOfnumberbooleanstring": "Object", // number/boolean/string
    "OneOfintegerdouble": "Number",       // integer/double
    "integer": "Long"
  ]
  importMappings = [
    // Java representation of ambiguous imports
    'OneOfnumberbooleanstring': 'java.lang.Object',
    'OneOfintegerdouble': 'java.lang.Number',
  ]

  configOptions = [
    dateLibrary: 'leggacy',
    library: 'microprofile',
    openApiNullable: 'false',
    useOneOfInterfaces: 'true',
    legacyDiscriminatorBehavior: 'false',
  ]
  globalProperties = [
    // force generate only the models
    apis: 'false',
    invokers: 'false',
    models: '',
    modelDocs: 'false',
    modelTests: 'false'
  ]
}

def openApiSrcDir = "$buildDir/generated/src/main/java" as String
sourceSets {
  generatedApis {
    java.srcDirs openApiSrcDir
  }
}
compileGeneratedApisJava.dependsOn tasks.openApiGenerate
compileGeneratedApisJava {
  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7
}

dependencies {
  implementation deps.slf4j

  implementation project(':internal-api')

  compileOnly project(':communication')
  testImplementation project(':communication')

  compileOnly project(':utils:container-utils')
  testImplementation project(':utils:container-utils')

  compileOnly sourceSets.generatedApis.compileClasspath
  api sourceSets.generatedApis.output

  api deps.okhttp

  compileOnly deps.jnr_ffi
  testImplementation deps.jnr_ffi

  generatedApisImplementation deps.moshi

  testImplementation project(':utils:test-utils')
}

forbiddenApisGeneratedApis {
  signaturesFiles = files('/dev/null')
}
